ArchiveEntry "TRM Tangential Roundabout Maneuver".

/**
 * Essentials of Tangential Roundabout Maneuver (TRM) in Air Traffic Control (ATC)
 * @author Andre Platzer
 * @see "Andre Platzer. Differential-algebraic dynamic logic for differential-algebraic programs.Journal of Logic and Computation, 20(1), pages 309-352, 2010."
 */
ProgramVariables.
  R x1.   /* x-position of aircraft 1 */
  R x2.   /* y-position of aircraft 1 */
  R d1.   /* x-direction of aircraft 1 */
  R d2.   /* y-direction of aircraft 1 */
  R y1.   /* x-position of aircraft 2 */
  R y2.   /* y-position of aircraft 2 */
  R e1.   /* x-direction of aircraft 2 */
  R e2.   /* y-direction of aircraft 2 */
  R om.   /* angular velocity of aircraft 1 */
  R omy.  /* angular velocity of aircraft 2 */
  R c1.   /* x-position of center of roundabout circle */
  R c2.   /* y-position of center of roundabout circle */
  R p().  /* protected zone radius */
End.

Problem.
    (x1-y1)^2 + (x2-y2)^2 >= p()^2
  ->
   [{
      {
         om:=*;omy:=*;
         {x1'=d1,x2'=d2, d1'=-om*d2,d2'=om*d1, y1'=e1,y2'=e2, e1'=-omy*e2,e2'=omy*e1 &
          ((x1-y1)^2 + (x2-y2)^2 >= p()^2)}
       }*@invariant((x1-y1)^2 + (x2-y2)^2 >= p()^2);
       c1:=*;c2:=*; om:=*;
       d1:=-om*(x2-c2); d2:=om*(x1-c1);
       e1:=-om*(y2-c2); e2:=om*(y1-c1);
       {x1'=d1,x2'=d2, d1'=-om*d2,d2'=om*d1, y1'=e1,y2'=e2, e1'=-om*e2,e2'=om*e1}
       /* @invariant(d1-e1=-om*(x2-y2)&d2-e2=om*(x1-y1)) */
    }*@invariant((x1-y1)^2 + (x2-y2)^2 >= p()^2)
  ] (x1-y1)^2 + (x2-y2)^2 >= p()^2
End.

Tactic "TRM Tangential Roundabout Maneuver: Interactive Proof".
  implyR(1) ; loop({`(x1-y1)^2+(x2-y2)^2>=p()^2`}, 1) ; <(
  closeId,
  closeId,
  composeb(1) ; loop({`(x1-y1)^2+(x2-y2)^2>=p()^2`}, 1) ; <(
    closeId,
    composeb(1) ; randomb(1) ; allR(1) ; composeb(1) ; randomb(1) ; allR(1) ; composeb(1) ; randomb(1) ; allR(1) ; composeb(1) ; assignb(1) ; composeb(1) ; assignb(1) ; composeb(1) ; assignb(1) ; composeb(1) ; assignb(1) ; dC({`d1-e1=-om*(x2-y2)&d2-e2=om*(x1-y1)`}, 1) ; <(
      dI(1),
      dI(1)
      ),
    composeb(1) ; randomb(1) ; allR(1) ; composeb(1) ; randomb(1) ; allR(1) ; dW(1) ; prop
    )
  )
End.
       
Tactic "TRM Tangential Roundabout Maneuver: Automatic Proof".
  master
End.
       

End.
       