ArchiveEntry "LMPC".

Functions.        /* function symbols cannot change their value */
      R b().          /* braking capabilities */
      R A().           /* maximum acceleration */
      R ep().         /* reaction time */
    End.
    
    ProgramVariables. /* program variables may change their value over time */
      R x.            /* position of the car */
      R v.            /* velocity of the car */
      R a.            /* acceleration of the car */
      R m.            /* stop sign position */
      R t.            /* clock variable */
    End.
    
    Problem.          /* differential dynamic logic formula */
      [{x'=v, v'=-b}](x<=m)
       & v >= 0  
       & A >= 0
       & b > 0
    ->
      [
        {
        {{?([t:=0; {x'=v, v'=A, t'=1 & v >= 0 & t<=ep}] [{x'=v, v'=-b}](x<=m));
           a := A;}
        ++ a := -b;}
          t := 0;
          {x'=v, v'=a, t'=1 & v>=0 & t<=ep}
        }*@invariant([{x'=v, v'=-b}](x<=m))
      ] (x <= m)
    End.

Tactic "LMPC: Proof".
  implyR(1) ; loop({`[{x'=v,v'=-b()&true}]x<=m`}, 1) ; <(
  closeId,
  boxd(-1) ; notL(-1) ; solve(2) ; existsR({`0`}, 2) ; andR(2) ; <(
    QE,
    notR(2) ; QE
    ),
  composeb(1) ; choiceb(1) ; andR(1) ; <(
    composeb(1) ; testb(1) ; implyR(1) ; assignb(1) ; closeId,
    assignb(1) ; composeb(1) ; assignb(1) ; solve(-1) ; MR({`\forall t_ (t_>=0->(-b())/2*t_^2+v*t_+x<=m)`}, 1) ; <(
      solve(1) ; QE,
      solve(1) ; QE
      )
    )
  )
End.
       

End.
