ArchiveEntry "Parachute".

Functions.
  R g() = (3).  /* square root of gravity */
  R p() = (1).  /* square root of parachute drag coefficient */
  R a().        /* skydiver air drag coefficient */
  R m().        /* impact velocity */
  R T().        /* skydiver reaction time */
End.

ProgramVariables.
  R x.    /* skydiver altitude */
  R v.    /* skydiver speed (< 0, so lower is faster) */
  R r.    /* skydiver current drag coefficient (depends on parachute open/closed) */
  R t.    /* time */
End.

Problem.
  g()>0 & p()>0 & p()^2>a() & a()>0 & T()>0 & m()< -g()/p() &
  x>=0 & v<0 & v> -g()/p() & r=a()
->
[ {
    {
      ?(v - g()^2*T() > -g()/p() & r = a());
      ++
      r := p()^2;
    }
    t := 0;
    {x'=v, v'=-g()^2+r*v^2, t'=1 & t<=T() & x>=0 & v<0}
  }*@invariant((x>=0 & v<0) & v>-g()/p())
](x=0 -> v>=m())
End.

Tactic "Parachute: Interactive Proof".
  implyR(1) ; loop({`(x>=0&v<0)& v>-g()/p()`}, 1) ; <(
  QE,
  QE,
  boxAnd(1) ; andR(1) ; <(
    master,
    unfold ; <(
      dC({`v>=old(v)-g()^2*t`}, 1) ; <(
        dW(1) ; QE,
        dI(1)
        ),
      dG({`y'=(-1/2*1^2*(v-g()/p()))*y`}, {`y^2*(v+g()/p())=1`}, 1) ; dI(1.0) ; QE
      )
    )
  )
End.
       

End.
       