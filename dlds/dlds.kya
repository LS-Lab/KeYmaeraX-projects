ArchiveEntry "LICS: 1 Continuous car accelerates forward".

ProgramVariables. /* program variables may change their value over time */
  R x.            /* position of the car */
  R v.            /* velocity of the car */
  R a.            /* acceleration of the car */
End.

Problem.          /* differential dynamic logic formula */
    v>=0 & a>=0
 -> [
      {x'=v, v'=a & v>=0}
    ] v>=0
End.

Tactic "LICS: 1 Continuous car accelerates forward: Clever Proof".
  implyR(1) ; dW(1) ; prop; done
End.

Tactic "LICS: 1 Continuous car accelerates forward: Interactive Proof".
  implyR(1) ; solve(1) ; QE
End.
       

End.
ArchiveEntry "LICS: 2 Single car drives forward".

Functions.        /* function symbols cannot change their value */
  R b().          /* braking capabilities */
  R A().          /* maximum acceleration */
End.

ProgramVariables. /* program variables may change their value over time */
  R x.            /* position of the car */
  R v.            /* velocity of the car */
  R a.            /* acceleration of the car */
End.

Problem.          /* differential dynamic logic formula */
    v>=0  & A>=0 & b>0
 -> [
      {
        {a:=A; ++ a:=-b;}
        {x'=v, v'=a & v>=0}
      }*@invariant(v>=0)
    ] v>=0
End.

Tactic "LICS: 2 Single car drives forward: Clever Proof".
implyR(1) ; loop({`v>=0`}, 1) ; <(
  closeId,
  closeId,
  composeb(1) ; choiceb(1) ; andR(1) ; <(
    assignb(1) ; dW(1) ; prop ; done,
    assignb(1) ; dW(1) ; prop ; done
    )
  )
End.

Tactic "LICS: 2 Single car drives forward: Interactive Proof".
  implyR(1) ; loop({`v>=0`}, 1) ; <(
  closeId,
  closeId,
  composeb(1) ; choiceb(1) ; andR(1) ; <(
    assignb(1) ; solve(1) ; QE,
    assignb(1) ; solve(1) ; QE
    )
  )
End.
       
Tactic "LICS: 2 Single car drives forward: Automatic Proof".
  master
End.
       

End.
ArchiveEntry "LICS: 4a safe stopping of time-triggered car".

Functions.        /* function symbols cannot change their value */
  R b().          /* braking capabilities */
  R A().          /* maximum acceleration */
  R ep().         /* reaction time */
End.

ProgramVariables. /* program variables may change their value over time */
  R x.            /* position of the car */
  R v.            /* velocity of the car */
  R a.            /* acceleration of the car */
  R m.            /* stop sign position */
  R t.            /* clock variable */
End.

Problem.          /* differential dynamic logic formula */
    v^2<=2*b*(m-x) & v>=0  & A>=0 & b>0
 -> [
      {
        {?(2*b*(m-x) >= v^2+(A+b)*(A*ep^2+2*ep*v)); a:=A; ++ a:=-b; }
        t := 0;
        {x'=v, v'=a, t'=1 & v>=0 & t<=ep}
      }*@invariant(v^2<=2*b*(m-x))
    ] x <= m
End.

Tactic "LICS: 4a safety of time-triggered car: Clever Proof".
implyR(1) ; loop({`v^2<=2*b()*(m-x)`}, 1) ; <(
  closeId,
  QE,
  composeb(1) ; choiceb(1) ; andR(1) ; <(
    composeb(1) ; testb(1) ; implyR(1) ; assignb(1) ; composeb(1) ; assignb(1) ; dC({`2*b()*(m-x)>=v^2+(A()+b())*(A()*(ep()-t)^2+2*(ep()-t)*v)`}, 1) ; <(
      dW(1) ; QE,
      dI(1)
      ),
    assignb(1) ; composeb(1) ; assignb(1) ; dI(1)
    )
  )
End.

Tactic "LICS: 4a safety of time-triggered car: Interactive Proof".
  implyR(1) ; loop({`v^2<=2*b()*(m-x)`}, 1) ; <(
  QE,
  QE,
  composeb(1) ; choiceb(1) ; andR(1) ; <(
    composeb(1) ; testb(1) ; implyR(1) ; assignb(1) ; composeb(1) ; assignb(1) ; solve(1) ; QE,
    assignb(1) ; composeb(1) ; assignb(1) ; solve(1) ; QE
    )
  )
End.
       
Tactic "LICS: 4a safety of time-triggered car: Automatic Proof".
  master
End.
       

End.
ArchiveEntry "Bouncing Ball".

Functions.        /* function symbols cannot change their value */
  R H().          /* initial height */
  R g().          /* gravity */
  R c().          /* damping coefficient */
End.

ProgramVariables. /* program variables may change their value over time */
  R x.            /* height */
  R v.            /* velocity */
End.

Problem.
  x>=0 & x=H
  & v=0 & g>0 & 1>=c&c>=0
 ->
  [
    {
      {x'=v,v'=-g&x>=0}
      {?x=0; v:=-c*v;  ++  ?x!=0;}
    }*@invariant(2*g*x<=2*g*H-v^2 & x>=0)
  ] (x>=0 & x<=H)
End.

Tactic "Bouncing Ball: Clever Proof".
implyR(1) ; loop({`2*g()*x<=2*g()*H()-v^2&x>=0`}, 1) ; <(
  QE,
  QE,
  composeb(1) ; dC({`2*g()*x<=2*g()*H()-v^2`}, 1) ; <(
    dW(1) ; implyR(1) ; choiceb(1) ; andR(1) ; <(
      composeb(1) ; testb(1) ; implyR(1) ; assignb(1) ; QE,
      testb(1) ; implyR(1) ; prop ; done
      ),
    dI(1)
    )
  )
End.

Tactic "Bouncing Ball: generalizing as in book".
  implyR(1) ; loop({`2*g()*x<=2*g()*H()-v^2&x>=0`}, 1) ; <(
QE, 
QE, 
composeb(1) ; MR({`2*g()*x<=2*g()*H()-v^2&x>=0&1>=c()&c()>=0&g()>0`}, 1) ; <(
solve(1) ; QE, 
  choiceb(1) ; andR(1) ; <(
  composeb(1) ; testb(1) ; implyR(1) ; assignb(1) ; QE, 
    testb(1) ; prop
  )
)
)
End.
       
Tactic "Bouncing Ball: top-level".
  implyR(1) ; loop({`2*g()*x<=2*g()*H()-v^2&x>=0`}, 1) ; <(
QE, 
QE, 
composeb(1) ; solve(1) ; allR(1) ; implyR(1) ; implyR(1) ; allL({`t_`}, -6) ; allR(1) ; implyR(1) ; choiceb(1) ; andR(1) ; <(
composeb(1) ; testb(1) ; implyR(1) ; assignb(1) ; QE, 
  testb(1) ; implyR(1) ; QE
)
)
End.

Tactic "Bouncing Ball: in-place proof".
  implyR(1) ; loop({`2*g()*x<=2*g()*H()-v^2&x>=0`}, 1) ; <(
QE, 
QE, 
composeb(1) ; choiceb(1.1) ; composeb(1.1.0) ; testb(1.1.0) ; assignb(1.1.0.1) ; testb(1.1.1) ; solve(1) ; QE
)
End.
       
Tactic "Bouncing Ball: automatic".
  master
End.
       

End.
ArchiveEntry "Simple rotational dynamics".

ProgramVariables.
  R v.  /* direction in x-axis */
  R w.  /* direction in y-axis */
  R r.  /* radius r of circle around origin */
End.

Problem.
  v^2+w^2-r^2=0 -> 
    [{v'=w,w'=-v}@invariant(v^2+w^2-r^2=0)] v^2+w^2-r^2=0
End.

Tactic "10: Simple rotational dynamics: Proof 1".
  implyR(1) ; dI(1)
End.
       

End.
ArchiveEntry "Damped Oscillator".

Functions.        /* function symbols cannot change their value */
  R w().          /* undamped angular frequency */
  R c().          /* level of ellipse */
  R d().          /* constant damping ratio */
End.

ProgramVariables. /* program variables may change their value over time */
  R x.            /* position of spring/mass system */
  R y.            /* velocity */
End.

Problem.
    w()^2*x^2 + y^2 <= c()^2
->
  [{x'=y, y'=-w()^2*x-2*d()*w()*y & w()>=0 & d()>=0}]w()^2*x^2 + y^2 <= c()^2
End.

Tactic "Damped Oscillator: Differential invariant proof".
  implyR(1) ; dI(1)
End.

End.


ArchiveEntry "Increasingly Damped Oscillator".

Functions.
  R w().    /* undamped angular frequency */
  R c().    /* level of ellipse */
End.

ProgramVariables.
  R x.      /* position of spring/mass system */
  R y.      /* velocity */
  R d.      /* damping ratio */
End.

Problem.
    w()^2*x^2 + y^2 <= c()^2
  & d>=0
->
  [{x'=y, y'=-w()^2*x-2*d*w()*y, d'=7 & w()>=0}]w()^2*x^2 + y^2 <= c()^2
End.

Tactic "Increasingly Damped Oscillator: Differential cut proof".
  implyR(1) ; dC({`d>=0`}, 1) ; <(
    dI(1), 
    dI(1)
  )
End.
       

End.
ArchiveEntry "Exponential Decay: [x'=-x]x>0".

/**
 * Exponential Decay.
 */
ProgramVariables.
  R x.
End.

Problem.
  x>1 -> [{x'=-x}]x>0
End.

Tactic "Exponential Decay from tactic".
  implyR(1) ; dG({`y`}, {`1/2`}, {`0`}, {`x*y^2=1`}, 1) ; dI(1.0) ; QE
End.
 
Tactic "Exponential Decay: Automatic".
  master
End.
 

End.

ArchiveEntry "Exponential Growth: [x'=x]x>0".

/**
 * Exponential Growth.
 */
ProgramVariables.
  R x.
End.

Problem.
  x>1 -> [{x'=x}]x>0
End.

Tactic "Exponential Growth from tactic".
  implyR(1) ; dG({`y`}, {`-1/2`}, {`0`}, {`x*y^2=1`}, 1) ; dI(1.0) ; QE
End.

Tactic "Exponential Growth: Automatic".
  master
End.


End.


ArchiveEntry "Exponential at Equilibrium [x'=x]x=0".

/**
* Exponential at equilibrium.
*/
ProgramVariables.
 R x.
End.

Problem.
 x=0 -> [{x'=x}]x=0
End.


Tactic "Exponential at Equilibrium from tactic".
 implyR(1) ; dG({`y`}, {`-1`}, {`0`}, {`y>0&x*y=0`}, 1) ; boxAnd(1.0) ; dI(1.0.1) ; dG({`z`}, {`1/2`}, {`0`}, {`y*z^2=1`}, 1.0.0) ; dI(1.0.0.0) ; QE
End.

Tactic "Exponential at Equilibrium: top-level proof".
 implyR(1) ; dG({`y`}, {`-1`}, {`0`}, {`y>0&x*y=0`}, 1) ; existsR({`1`}, 1) ; boxAnd(1) ; andR(1) ; <(
 dG({`z`}, {`1/2`}, {`0`}, {`z^2*y=1`}, 1) ; existsR({`1`}, 1) ; dI(1),
 dI(1)
 )
End.

Tactic "Exponential at Equilibrium: Automatic".
 master
End.


End.

ArchiveEntry "Exponential difference [x'=x]x!=0".

/**
 * Exponential difference.
 */
ProgramVariables.
  R x.
End.

Problem.
  x!=0 -> [{x'=x}]x!=0
End.


Tactic "Exponential difference from tactic".
  implyR(1) ; dG({`y`}, {`-1`}, {`0`}, {`x*y=1`}, 1) ; dI(1.0) ; QE
End.
   
Tactic "Exponential difference: top-level proof".
  implyR(1) ; dG({`y`}, {`-1`}, {`0`}, {`x*y=1`}, 1) ; existsR({`1/x`}, 1) ; dI(1)
End.
   
End.

ArchiveEntry "Parachute".

Functions.
  R g() = (9).  /* gravity */
  R p() = (1).  /* parachute drag coefficient */
  R a().        /* skydiver air drag coefficient */
  R m().        /* impact velocity */
  R T().        /* skydiver reaction time */
End.

ProgramVariables.
  R x.    /* skydiver altitude */
  R v.    /* skydiver speed (< 0, so lower is faster) */
  R r.    /* skydiver current drag coefficient (depends on parachute open/closed) */
  R t.    /* time */
End.

Problem.
  g()>0 & p()>a() & a()>0 & T()>0 & m()< -(g()/p())^(1/2) &
  x>=0 & v<0 & v> -(g()/p())^(1/2) & r=a()
->
[ {
    {
      ?(v - g()*T() > -(g()/p())^(1/2) & r = a());
      ++
      r := p();
    }
    t := 0;
    {x'=v, v'=-g()+r*v^2, t'=1 & t<=T() & x>=0 & v<0}
  }*@invariant((x>=0 & v<0) & v>-(g()/p())^(1/2))
](x=0 -> v>=m())
End.

Tactic "Parachute: Proof 1".
  implyR(1) ; loop({`(x>=0&v < 0)&v>-(9/1)^(1/2)`}, 1) ; <(
  QE,
  QE,
  boxAnd(1) ; andR(1) ; <(
    master,
    unfold ; <(
      dC({`v>=old(v)-9*t`}, 1) ; <(
        dW(1) ; QE,
        dI(1)
        ),
      dG({`y`}, {`-1/2*1*(v-(9/1)^(1/2))`}, {`0`}, {`y^2*(v+(9/1)^(1/2))=1`}, 1) ; dI(1.0) ; QE
      )
    )
  )
End.
       

End.
       