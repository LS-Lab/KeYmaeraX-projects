Theorem "lyapunov stability of origin (locally asymptotically stable)"

Description "stability of the origin for a planar system without radial unboundednes".

Citation "Wassim M. Haddad & VijaySekhar Chellaboina. Nonlinear Dynamical Systems and Control A Lyapunov-Based Approach. Example 3.3".

ProgramVariables
  Real x1;
  Real x2;
End.

Problem
\forall e (
  e > 0 ->
  \exists d
  (d > 0 &
  \forall x1 \forall x2
  (
  x1^2+x2^2 < d ->
  [
    {
    x1' = - 6*x1/(1+x1^2)^2 + 2*x2,
    x2' = - 2*(x1+x2)/(1+x1^2)^2
    }
  ]
  x1^2+x2^2 < e
  )
  )
)
End.

Tactic "lyapunov stability of origin (locally asymptotically stable): Proof"
unfold ; cut("\exists k (k>0&\forall x1 \forall x2 (x1^2+x2^2=e->x1^2/(1+x1^2)+x2^2>=k))") ; <(
unfold ; cut("\exists d (d>0&\forall x1 \forall x2 (x1^2+x2^2 < d->x1^2/(1+x1^2)+x2^2 < k))") ; <(
unfold ; existsR("d", 1) ; unfold ; allL("x1", -5) ; allL("x2", -5) ; implyL(-5) ; <(
  master,
    dC("x1^2/(1+x1^2)+x2^2 < k", 1) ; <(
    ODE(1),
      ODE(1)
    )
  ),
  hideR(1) ; QE
),
hideR(1) ; QE
)
End.

End.

Theorem "lyapunov stability of origin (globally asymptotically stable)"

Description "stability of the origin for a planar system with radial unboundedness".

Citation "Wassim M. Haddad & VijaySekhar Chellaboina. Nonlinear Dynamical Systems and Control A Lyapunov-Based Approach. Example 3.4".

ProgramVariables
  Real x1;
  Real x2;
End.

Problem
\forall e (
  e > 0 ->
  \exists d
  (d > 0 &
  \forall x1 \forall x2
  (
  x1^2+x2^2 < d ->
  [
    {
    x1' = - x1 + x2^3,
    x2' = - x1 - x2
    }
  ]
  x1^2+x2^2 < e
  )
  )
)

End.

Tactic "lyapunov stability of origin (globally asymptotically stable): Proof"
unfold ; cut("\exists k (k>0&\forall x1 \forall x2 (x1^2+x2^2=e->x1^2/2+x2^4/4>=k))") ; <(
  unfold ; cut("\exists d (d>0&\forall x1 \forall x2 (x1^2+x2^2 < d->x1^2/2+x2^4/4 < k))") ; <(
    unfold ; existsR("d", 1) ; unfold ; allL("x1", -5) ; allL("x2", -5) ; implyL(-5) ; <(
      master,
      dC("x1^2/2+x2^4/4 < k", 1) ; <(
        ODE(1),
        ODE(1)
        )
      ),
    hideR(1=="\exists d (d>0&\forall x1 \forall x2 (x1^2+x2^2 < d->[{x1'=-x1+x2^3,x2'=-x1-x2&true}]x1^2+x2^2 < e))") ; QE
    ),
  hideR(1=="\exists d (d>0&\forall x1 \forall x2 (x1^2+x2^2 < d->[{x1'=-x1+x2^3,x2'=-x1-x2&true}]x1^2+x2^2 < e))") ; QE
  )
End.

End.

/* Exported from KeYmaera X v4.7.2 */

Theorem "global exponential stability of origin"

Description "global exponential stability of the origin for a planar system".

Citation "Hassan K. Khalil. Nonlinear Systems. Exercise 4.20 (b)".

ProgramVariables
  Real x1;
  Real x2;
  Real  y;  /* Auxiliary used to encode the decaying exponential bound */
End.

Problem

\exists a (
\exists b (
  a > 0 & b > 0 &
  \forall x1 \forall x2
  (
  [
    y := a * (x1^2+x2^2) ;
    {
      x1' = -x1 - x2,
      x2' = x1 - x2,
      y'  = -b * y   /* Technically can put a factor of 2 here */
    }
  ]
  x1^2+x2^2 <= y /* This proves x1^2 + x2^2 <= a*old(x1^2+x2^2) e^(-bt) */
  )
)
)
End.

Tactic "global exponential stability of origin: Proof"
chaseAt(1) ; existsR("2", 1) ; chaseAt(1) ; existsR("1", 1) ; unfold ; <(
  QE,
  QE,
  dC("x1^2+2*x2^2-y<=0", 1) ; <(
    ODE(1),
    ODE(1)
    )
  )
End.

End.


/* Exported from KeYmaera X v4.7.1 */

Theorem "lyapunov stability of arbitrarily switched system"

Description "constructed example (based on preceding example)".

ProgramVariables
  Real x1;
  Real x2;
End.

Problem
\forall e (
  e > 0 ->
  \exists d
  (d > 0 &
  \forall x1 \forall x2
  (
  x1^2+x2^2 < d ->
  [
    {
      {
        x1' = - x1 + x2^3,
        x2' = - x1 - x2
      }
      ++
      {
        x1' = - x1,
        x2' = - x2
      }
    }*
  ]
  x1^2+x2^2 < e
  )
  )
)

End.

Tactic "lyapunov stability of arbitrarily switched system: Proof"
unfold ; cut("\exists k (k>0&\forall x1 \forall x2 (x1^2+x2^2=e->x1^2/2+x2^4/4>=k))") ; <(
  unfold ; cut("\exists d (e>d&d>0&\forall x1 \forall x2 (x1^2+x2^2 < d->x1^2/2+x2^4/4 < k))") ; <(
    unfold ; existsR("d", 1) ; unfold ; allL("x1", -6) ; allL("x2", -6) ; implyL(-6) ; <(
      propAuto,
      loop("x1^2/2+x2^4/4 < k&x1^2+x2^2 < e", 1) ; <(
        hideL(-3=="\forall x1 \forall x2 (x1^2+x2^2=e->x1^2/2+x2^4/4>=k)") ; QE,
        propAuto,
        unfold ; <(
          ODE(1),
          ODE(1)
          )
        )
      ),
    hideR(1=="\exists d (d>0&\forall x1 \forall x2 (x1^2+x2^2 < d->[{{x1'=-x1+x2^3,x2'=-x1-x2&true}++{x1'=-x1,x2'=-x2&true}}*]x1^2+x2^2 < e))") ; QE
    ),
  hideR(1=="\exists d (d>0&\forall x1 \forall x2 (x1^2+x2^2 < d->[{{x1'=-x1+x2^3,x2'=-x1-x2&true}++{x1'=-x1,x2'=-x2&true}}*]x1^2+x2^2 < e))") ; QE
  )
End.

End.

/* Exported from KeYmaera X v4.7.1 */

Theorem "lyapunov stability under constrained switching (common lyapunov function)"

Description "state-dependent switched system: a different state-dependent switching can lead to an unstable system (see citation)".

Citation "Daniel Liberzon. Switching In Systems And Control. Example 3.1".

ProgramVariables
  Real x1;
  Real x2;
End.

Problem
\forall e (
  e > 0 ->
  \exists d
  (d > 0 &
  \forall x1 \forall x2
  (
  x1^2+x2^2 < d ->
  [
    {
      {
        x1' = -1/2 * x1 - x2,
        x2' =    2 * x1 - 1/2 * x2
        & x1*x2 <= 0
      }
      ++
      {
        x1' = -1/2 * x1 - 2 * x2,
        x2' =        x1 - 1/2 * x2
        & x1*x2 >= 0
      }
    }*
  ]
  x1^2+x2^2 < e
  )
  )
)

End.

Tactic "lyapunov stability under constrained switching (common lyapunov function): Proof"
unfold ; existsR("e", 1) ; unfold ; master
End.

End.

/* Exported from KeYmaera X v4.7.1 */

Theorem "lyapunov stability under constrained switching (individual lyapunov functions)"

Description "state-dependent switched system: no common lyapunov function for sub-systems".

Citation "Daniel Liberzon. Switching In Systems And Control. Example 3.2".

ProgramVariables
  Real x1;
  Real x2;
End.

Problem
\forall e (
  e > 0 ->
  \exists d
  (d > 0 &
  \forall x1 \forall x2
  (
  x1^2+x2^2 < d ->
  [
    {
      {
        x1' = -1/2 * x1 - x2,
        x2' =    2 * x1 - 1/2 * x2
        & x1 >= 0
      }
      ++
      {
        x1' = -1/2 * x1 - 2 * x2,
        x2' =        x1 - 1/2 * x2
        & x1 <= 0
      }
    }*
  ]
  x1^2+x2^2 < e
  )
  )
)

End.

Tactic "lyapunov stability under constrained switching (individual lyapunov functions): Proof"
unfold ; cut("\exists k (k>0&\forall x1 \forall x2 (x1^2+x2^2=e->2*x1^2+x2^2>=k&1/2*x1^2+x2^2>=k))") ; <(
  unfold ; cut("\exists d (e>d&d>0&\forall x1 \forall x2 (x1^2+x2^2 < d->2*x1^2+x2^2 < k&1/2*x1^2+x2^2 < k))") ; <(
    unfold ; existsR("d", 1) ; unfold ; allL("x1", -6) ; allL("x2", -6) ; implyL(-6) ; <(
      propAuto,
      loop("2*x1^2+x2^2 < k&x1^2+x2^2 < e&x1>=0|1/2*x1^2+x2^2 < k&x1^2+x2^2 < e&x1<=0", 1) ; <(
        QE,
        propAuto,
        unfold ; <(
          orL(-1) ; <(
            MR("2*x1^2+x2^2 < k&x1^2+x2^2 < e&x1>=0", 1) ; <(
              ODE(1),
              propAuto
              ),
            unfold ; MR("2*x1^2+x2^2 < k&x1^2+x2^2 < e&x1>=0", 1) ; <(
              diffUnpackEvolDomain(1) ; ODE(1),
              propAuto
              )
            ),
          orL(-1) ; <(
            diffUnpackEvolDomain(1) ; unfold ; MR("1/2*x1^2+x2^2 < k&x1^2+x2^2 < e&x1<=0", 1) ; <(
              ODE(1),
              propAuto
              ),
            MR("1/2*x1^2+x2^2 < k&x1^2+x2^2 < e&x1<=0", 1) ; <(
              ODE(1),
              propAuto
              )
            )
          )
        )
      ),
    hideR(1=="\exists d (d>0&\forall x1 \forall x2 (x1^2+x2^2 < d->[{{x1'=-1/2*x1-x2,x2'=2*x1-1/2*x2&x1>=0}++{x1'=-1/2*x1-2*x2,x2'=x1-1/2*x2&x1<=0}}*]x1^2+x2^2 < e))") ; hideL(-3=="\forall x1 \forall x2 (x1^2+x2^2=e->2*x1^2+x2^2>=k&1/2*x1^2+x2^2>=k)") ; QE
    ),
  hideR(1=="\exists d (d>0&\forall x1 \forall x2 (x1^2+x2^2 < d->[{{x1'=-1/2*x1-x2,x2'=2*x1-1/2*x2&x1>=0}++{x1'=-1/2*x1-2*x2,x2'=x1-1/2*x2&x1<=0}}*]x1^2+x2^2 < e))") ; QE
  )
End.

End.
