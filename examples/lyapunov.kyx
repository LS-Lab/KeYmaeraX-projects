Theorem "lyapunov stability of origin (locally asymptotically stable)"

Description "stability of the origin for a planar system without radial unboundednes".

Citation "Wassim M. Haddad & VijaySekhar Chellaboina. Nonlinear Dynamical Systems and Control A Lyapunov-Based Approach. Example 3.3".

ProgramVariables
  Real x1;
  Real x2;
End.

Problem
\forall e (
  e > 0 ->
  \exists d
  (d > 0 &
  \forall x1 \forall x2
  (
  x1^2+x2^2 < d ->
  [
    {
    x1' = - 6*x1/(1+x1^2)^2 + 2*x2,
    x2' = - 2*(x1+x2)/(1+x1^2)^2
    }
  ]
  x1^2+x2^2 < e
  )
  )
)
End.

Tactic "lyapunov stability of origin (locally asymptotically stable): Proof"
unfold ; cut({`\exists k (k>0&\forall x1 \forall x2 (x1^2+x2^2=e->x1^2/(1+x1^2)+x2^2>=k))`}) ; <(
unfold ; cut({`\exists d (d>0&\forall x1 \forall x2 (x1^2+x2^2 < d->x1^2/(1+x1^2)+x2^2 < k))`}) ; <(
unfold ; existsR({`d`}, 1) ; unfold ; allL({`x1`}, -5) ; allL({`x2`}, -5) ; implyL(-5) ; <(
  master,
    dC({`x1^2/(1+x1^2)+x2^2 < k`}, 1) ; <(
    ODE(1),
      ODE(1)
    )
  ),
  hideR(1) ; QE
),
hideR(1) ; QE
)
End.

End.

Theorem "lyapunov stability of origin (globally asymptotically stable)"

Description "stability of the origin for a planar system with radial unboundedness".

Citation "Wassim M. Haddad & VijaySekhar Chellaboina. Nonlinear Dynamical Systems and Control A Lyapunov-Based Approach. Example 3.4".

ProgramVariables
  Real x1;
  Real x2;
End.

Problem
\forall e (
  e > 0 ->
  \exists d
  (d > 0 &
  \forall x1 \forall x2
  (
  x1^2+x2^2 < d ->
  [
    {
    x1' = - x1 + x2^3,
    x2' = - x1 - x2
    }
  ]
  x1^2+x2^2 < e
  )
  )
)

End.

Tactic "lyapunov stability of origin (globally asymptotically stable): Proof"
unfold ; cut({`\exists k (k>0&\forall x1 \forall x2 (x1^2+x2^2=e->x1^2/2+x2^4/4>=k))`}) ; <(
  unfold ; cut({`\exists d (d>0&\forall x1 \forall x2 (x1^2+x2^2 < d->x1^2/2+x2^4/4 < k))`}) ; <(
    unfold ; existsR({`d`}, 1) ; unfold ; allL({`x1`}, -5) ; allL({`x2`}, -5) ; implyL(-5) ; <(
      master,
      dC({`x1^2/2+x2^4/4 < k`}, 1) ; <(
        ODE(1),
        ODE(1)
        )
      ),
    hideR(1=={`\exists d (d>0&\forall x1 \forall x2 (x1^2+x2^2 < d->[{x1'=-x1+x2^3,x2'=-x1-x2&true}]x1^2+x2^2 < e))`}) ; QE
    ),
  hideR(1=={`\exists d (d>0&\forall x1 \forall x2 (x1^2+x2^2 < d->[{x1'=-x1+x2^3,x2'=-x1-x2&true}]x1^2+x2^2 < e))`}) ; QE
  )
End.

End.

Theorem "lyapunov stability of arbitrarily switched system"

Description "constructed example (based on preceding example)".

ProgramVariables
  Real x1;
  Real x2;
End.

Problem
\forall e (
  e > 0 ->
  \exists d
  (d > 0 &
  \forall x1 \forall x2
  (
  x1^2+x2^2 < d ->
  [
    {
      {
        x1' = - x1 + x2^3,
        x2' = - x1 - x2
      }
      ;
      {
        x1' = - x1,
        x2' = - x2
      }
    }*
  ]
  x1^2+x2^2 < e
  )
  )
)

End.

Tactic "lyapunov stability of arbitrarily switched system: Proof"
unfold ; cut("\exists k (k>0&\forall x1 \forall x2 (x1^2+x2^2=e->x1^2/2+x2^4/4>=k))") ; <(
  unfold ; cut("\exists d (e>d&d>0&\forall x1 \forall x2 (x1^2+x2^2 < d->x1^2/2+x2^4/4 < k))") ; <(
    unfold ; existsR("d", 1) ; unfold ; allL("x1", -6) ; allL("x2", -6) ; implyL(-6) ; <(
      master,
      loop("x1^2/2+x2^4/4 < k&x1^2+x2^2 < e", 1) ; <(
        hideL(-3=="\forall x1 \forall x2 (x1^2+x2^2=e->x1^2/2+x2^4/4>=k)") ; QE,
        QE,
        composeb(1) ; dC("x1^2/2+x2^4/4 < k&x1^2+x2^2 < e", 1) ; <(
          dW(1) ; hideL(-1=="x1_0^2/2+x2_0^4/4 < k&x1_0^2+x2_0^2 < e") ; unfold ; ODE(1),
          ODE(1)
          )
        )
      ),
    hideR(1=="\exists d (d>0&\forall x1 \forall x2 (x1^2+x2^2 < d->[{{x1'=-x1+x2^3,x2'=-x1-x2&true}{x1'=-x1,x2'=-x2&true}}*]x1^2+x2^2 < e))") ; QE
    ),
  hideR(1=="\exists d (d>0&\forall x1 \forall x2 (x1^2+x2^2 < d->[{{x1'=-x1+x2^3,x2'=-x1-x2&true}{x1'=-x1,x2'=-x2&true}}*]x1^2+x2^2 < e))") ; QE
  )
End.

End.
