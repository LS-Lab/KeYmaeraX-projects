ArchiveEntry "Exponential Decay: [x'=-x]x>0"

ProgramVariables
  Real x;
End.

Problem
  x>1 -> [{x'=-x}]x>0
End.

Tactic "Exponential Decay from tactic"
  implyR(1) ; dG("y'=1/2*y", "x*y^2=1", 1) ; dI(1.0) ; QE
End.
 
Tactic "Exponential Decay: Automatic"
  /* needs QE("Mathematica") */
  master
End.

End.

ArchiveEntry "Exponential Growth: [x'=x]x>0"

ProgramVariables
  Real x;
End.

Problem
  x>1 -> [{x'=x}]x>0
End.

Tactic "Exponential Growth from tactic"
  implyR(1) ; dG("y'=-1/2*y", "x*y^2=1", 1) ; dI(1.0) ; QE
End.

Tactic "Exponential Growth: Automatic"
  master
End.

End.


ArchiveEntry "Exponential at Equilibrium [x'=x]x=0"

ProgramVariables
 Real x;
End.

Problem
 x=0 -> [{x'=x}]x=0
End.


Tactic "Exponential at Equilibrium from tactic"
 implyR(1) ; dG("y'=-1*y", "y>0&x*y=0", 1) ; boxAnd(1.0) ; dI(1.0.1) ; dG("z'=1/2*z", "y*z^2=1", 1.0.0) ; dI(1.0.0.0) ; QE
End.

Tactic "Exponential at Equilibrium: top-level proof"
 implyR(1) ; dG("y'=-1*y", "y>0&x*y=0", 1) ; existsR("1", 1) ; boxAnd(1) ; andR(1) ; <(
 dG("z'=1/2*z", "z^2*y=1", 1) ; existsR("1", 1) ; dI(1),
 dI(1)
 )
End.

Tactic "Exponential at Equilibrium: Automatic"
 /* needs QE("Mathematica") */
 master
End.

End.

ArchiveEntry "Exponential difference [x'=x]x!=0"

ProgramVariables
  Real x;
End.

Problem
  x!=0 -> [{x'=x}]x!=0
End.

Tactic "Exponential difference from tactic"
  implyR(1) ; dG("y'=-1*y", "x*y=1", 1) ; dI(1.0) ; QE
End.
   
Tactic "Exponential difference: top-level proof"
  implyR(1) ; dG("y'=-1*y", "x*y=1", 1) ; existsR("1/x", 1) ; dI(1)
End.
   
End.
