ArchiveEntry "Velocity Car"

Definitions       /* function symbols cannot change their value */
  Real V;         /* maximum velocity */
  Real ep;        /* reaction time */
End.

ProgramVariables  /* program variables may change their value over time */
  Real x;         /* position of the car */
  Real v;         /* velocity of the car */
  Real m;         /* stop sign position */
  Real t;         /* clock variable */
End.

Problem           /* differential dynamic logic formula */
    x<=m & V>=0
 -> [
      {
        {?x<=m-V*ep; v:=V; ++ v:=0;}
        t := 0;
        {x'=v, t'=1 & t<=ep}
      }* @invariant(x<=m)
    ] x <= m
End.

Tactic "Velocity Car: Proof 1"
  implyR(1) ; loop("x<=m", 1) ; <(
  id,
  id,
  composeb(1) ; composeb(1.1) ; choiceb(1) ; andR(1) ; <(
    composeb(1) ; testb(1) ; implyR(1) ; assignb(1) ; assignb(1) ; dC("x<=m-V*(ep-t)", 1) ; <(
      dW(1) ; QE,
      dI(1)
      ),
    assignb(1) ; assignb(1) ; dI(1)
    )
  )
End.

End.
       