ArchiveEntry "Kepler two body"
Description "Two planets in gravitational force field with one planet as origin, mathematically normalized constants".
Citation "Ernst Hairer. Geometric Numerical Integration. TU Munich 2010".

Definitions
  Real ec;       /* eccentricity */
  Real E;        /* generalized energy level */
End.

ProgramVariables
  Real x,y;      /* x/y coordinates of the planet */
  Real v,w;      /* x/y velocities of the planet */
End.

Problem
  /* 0<=ec&ec<1 &
  x=1-ec&v=0 & y=0&w=((1+ec)/(1-ec))^(1/2) & */
  (v^2+w^2)/2-1/(x^2+y^2)^(1/2) = E ->
   [{x'=v,v'=-x/(x^2+y^2)^(3/2),
     y'=w,w'=-y/(x^2+y^2)^(3/2)&(x!=0|y!=0)}] (v^2+w^2)/2-1/(x^2+y^2)^(1/2) = E
End.

Tactic "Kepler two body: Proof"
useSolver("Mathematica");
implyR(1) ; dI(1)
End.

Tactic "Kepler two body: Proof"
useSolver("Mathematica");
autoClose
End.

Illustration "https://lfcps.org/info/Kepler.png".
End.
