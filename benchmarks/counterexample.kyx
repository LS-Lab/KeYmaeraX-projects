ArchiveEntry "Unsound Barcan"

ProgramVariables
  Real x;
End.

Problem
  \forall x [x:=0;]x>=0 -> [x:=0;]\forall x x>=0
End.

End.

ArchiveEntry "Unsound G, V"

ProgramVariables
  Real x;
End.

Problem
  x>=0 -> [x:=x-1;]x>=0
End.

End.

ArchiveEntry "Counterexample False Constant"

ProgramVariables
  Real x;
  Real y;
End.

Problem
  x=y-> [{x'=1}]x=y
End.

End.

ArchiveEntry "Counterexample False Circular Invariant"

ProgramVariables
  Real x;
  Real y;
End.

Problem
  x=y-> [{x'=x,y'=-y}]x=y
End.

End.

ArchiveEntry "Counterexample 3.17 Variation"

Citation "Andre Platzer. Logical Analysis of Hybrid Systems: Proving Theorems for Complex Dynamics. Counterexample 3.17 variation".

Definitions
  Real v;
End.

ProgramVariables
  Real x1;
  Real x2;
  Real d1;
  Real d2;
  Real om;
End.

Problem
  (x1>=0 | d1^2+d2^2=v^2) & d1^2+d2^2=1 & om!=0 -> [{x1'=d1,x2'=d2,d1'=-om*d2,d2'=om*d1}](x1>=0 | d1^2+d2^2=v^2)
End.

End.

ArchiveEntry "Counterexample 3.18"

Citation "Andre Platzer. Logical Analysis of Hybrid Systems: Proving Theorems for Complex Dynamics. Counterexample 3.18".

ProgramVariables
  Real x;
End.

Problem
  x^2<=0 -> [{x'=1}]x^2<=0
End.

End.

ArchiveEntry "Counterexample 3.19"

Description "False differential induction".
Citation "Andre Platzer. Logical Analysis of Hybrid Systems: Proving Theorems for Complex Dynamics. Counterexample 3.19".

ProgramVariables
  Real x;
End.

Problem
  x!=0 -> [{x'=1}]x!=0
End.

End.

ArchiveEntry "Counterexample 3.19 Variation"

Description "False differential induction".
Citation "Andre Platzer. Logical Analysis of Hybrid Systems: Proving Theorems for Complex Dynamics. Counterexample 3.19".

ProgramVariables
  Real x;
  Real y;
End.

Problem
  x!=5 -> [{x'=1}]x!=5
End.

End.

ArchiveEntry "Counterexample 3.25"

Description "False differential induction".
Citation "Andre Platzer. Logical Analysis of Hybrid Systems: Proving Theorems for Complex Dynamics. Counterexample 3.25".

ProgramVariables
  Real x;
  Real y;
  Real a;
  Real b;
End.

Problem
  <{x'=a,y'=b}>x=y
End.

End.

ArchiveEntry "Counterexample 3.26"

Description "False exponential decay".
Citation "Andre Platzer. Logical Analysis of Hybrid Systems: Proving Theorems for Complex Dynamics. Counterexample 3.26".

ProgramVariables
  Real x;
End.

Problem
  <{x'=-x}>x<=0
End.

End.

ArchiveEntry "Counterexample 3.32"

Description "Initial value prevents progress".
Citation "Andre Platzer. Logical Analysis of Hybrid Systems: Proving Theorems for Complex Dynamics. Counterexample 3.32".

ProgramVariables
  Real x;
End.

Problem
  x=0 -> <{x'=1 & x<=1 | x>=2}>x>=3
End.

End.

ArchiveEntry "Counterexample 3.32 Variation"

Description "Initial value prevents progress".
Citation "Andre Platzer. Logical Analysis of Hybrid Systems: Proving Theorems for Complex Dynamics. Counterexample 3.32".

ProgramVariables
  Real x;
End.

Problem
  <x:=0; {x'=1 & x<=1 | x>=2}>x>=3
End.

End.

ArchiveEntry "False differential induction"

ProgramVariables
	Real x;
End.

Problem
	x>0 -> [{x'=-1}]x>0
End.

End.

ArchiveEntry "False loop induction (1)"

ProgramVariables
  Real x;
End.

Problem
  x=0 -> [{x:=x+1;}*]x<=1
End.

End.

ArchiveEntry "False loop induction (2)"

ProgramVariables
  Real x;
End.

Problem
  [x:=0; {x:=x+1;}*]x<=1
End.

End.

ArchiveEntry "False loop induction (3)"

ProgramVariables
  Real x;
  Real y;
End.

Problem
  x=0 & x=y -> [{x:=x+1;y:=1;}*](x<=1 | y=0)
End.

End.

ArchiveEntry "False loop induction (4)"

ProgramVariables
  Real x;
  Real y;
End.

Problem
  x=0 -> [{x:=x+1;y:=0;}*]x<=1 | y=0
End.

End.

ArchiveEntry "Primes in postcondition"

ProgramVariables
  Real x;
End.

Problem
  [{x'=1}]x'=1 -> x'=1
End.

End.

ArchiveEntry "Primes in evolution domain constraints (1)"

ProgramVariables
  Real x;
End.

Problem
  [{x'=2}]x'=5 <-> \forall t (t>=0 -> [x:=x+2*t;]x'=5)
End.

End.

ArchiveEntry "Primes in evolution domain constraints (2)"

ProgramVariables
  Real x;
End.

Problem
  [{x'=2 & x'>=5}]x<9 <-> \forall t (t>=0 -> (\forall s (0<=s & s<=t -> (x'>=5 -> [x:=x+2*t;]x<9))))
End.

End.

ArchiveEntry "Primes in evolution domain constraints (3)"

ProgramVariables
  Real x;
End.

Problem
  [{x'=2 & x'>=5}]false -> (x'>=5 -> false)
End.

End.

ArchiveEntry "Unsound renaming"

ProgramVariables
 Real x;
 Real y;
 Real z;
End.

Problem
  (x+y)'=x'+y' -> (x+y)'=z'+y'
End.

End.

ArchiveEntry "LICS: Example 3b event-triggered car is unsafe"

Citation "André Platzer. Logics of dynamical systems. ACM/IEEE Symposium on Logic in Computer Science, LICS 2012, June 25–28, 2012, Dubrovnik, Croatia, pp. 13-24. IEEE 2012.".
Link "http://symbolaris.com/pub/lds-lics.pdf#page=7".

ProgramVariables
	Real x;  /* x position of car */
	Real v;  /* v velocity of car */
	Real a;  /* a acceleration of car */
	Real b;  /* A max acceleration */
	Real A;  /* b max braking */
	Real m;  /* m position of Stop Sign */
End.

Problem
   ( v >= 0
	 & A >= 0
	 & b > 0 )
->
  [
    {
      {  ?(m-x>=2); a := A;
      ++ a := -b;
      };
      {x' = v, v' = a & v >= 0}
    }*@invariant(x<=m)
  ]x<=m
End.

Tactic "Proof should fail"
  /* assert tactic that checks master leaves open goals with counterexample/false */
  master
End.

End.
