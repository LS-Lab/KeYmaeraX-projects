ArchiveEntry "Switched damped oscillator"
Description "Parametric switching between two different damped oscillators".
Citation "Andre Platzer. Logical Analysis of Hybrid Systems: Proving Theorems for Complex Dynamics. Springer, 2010".

Definitions
  Real a;
  Real b;
End.

ProgramVariables
  Real x, y;
  Real c, d;
  Real w;
End.

Problem
    w>=0 & d>=0 & 
    -2<=a&a<=2 & b^2>=1/3 &
    w^2*x^2+y^2 <= c
  ->
    [{
      {x'=y,y'=-w^2*x-2*d*w*y};
      {  ?x=y*a; w:=2*w; d:=d/2; c := c*((2*w)^2+1^2)/(w^2+1^2);
      ++ ?x=y*b; w:=w/2; d:=2*d; c := c*(w^2+1^2)/((2*w^2)+1^2);
      ++ ?true;
      }
      }*@invariant(w^2*x^2+y^2<=c&d>=0&w>=0)
    ] w^2*x^2+y^2 <= c
End.
Tactic "Switched damped oscillator: Proof 1"
master ; loop("w^2*x^2+y^2<=c&d>=0&w>=0", 1) ; <(
  prop,
  prop,
  composeb(1) ; dC("w^2*x^2+y^2<=c", 1) ; <(
    dW(1) ; master,
    dI(1)
    )
  )
End.
End.